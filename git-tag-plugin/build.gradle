buildscript {
    dependencies {
        classpath 'org.ajoberstar:grgit:1.7.0'
    }
}

plugins {
    id 'uk.co.littlemike.publishing-smorgasbord-plugin' version '0.10'
    id 'com.gradle.plugin-publish' version '0.9.4'
    id 'java-gradle-plugin'
    id 'groovy'
}

version = '0.2'

repositories {
    jcenter()
}

dependencies {
    compile 'org.ajoberstar:grgit:1.7.0'

    testCompile 'junit:junit:4.12'
}

def pluginImplementation = 'uk.co.littlemike.gradle.git.GitTagPlugin'
basicPublishing {
    organisation = 'uk.co.littlemike'
    pluginImplementationClass = pluginImplementation
}

pluginBundle {
    website = 'https://github.com/LittleMikeDev/gradle-git-tag-plugin'
    vcsUrl = 'https://github.com/LittleMikeDev/gradle-git-tag-plugin.git'
    description = 'Plugin for simple tagging of releases in git'
    tags = ['simple', 'git', 'release', 'tag', 'tagging']
}

// Apply plugin to self
apply plugin: new GroovyScriptEngine(
        [file('src/main/groovy').absolutePath, file('src/main/resources').absolutePath].
                toArray(new String[2]),
        this.class.classLoader
).loadScriptByName(
        pluginImplementation.replaceAll(/\./, "/") + ".groovy"
)
